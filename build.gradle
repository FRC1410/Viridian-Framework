import edu.wpi.first.gradlerio.GradleRIOPlugin

plugins {
    id "java-library"
    id "edu.wpi.first.GradleRIO" version "2025.3.2"
}

group = "org.frc1410"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

def ROBOT_MAIN_CLASS = "robot.src.main.java.org.frc1410.rebuilt2026.Main"

repositories {
    mavenCentral()
}

// Deploy configuration for RoboRIO
deploy {
    targets {
        roborio(getTargetTypeClass("RoboRIO")) {
            team = 1410
            debug = project.frc.getDebugOrDefault(false)

            artifacts {
                frcJava(getArtifactTypeClass("FRCJavaArtifact")) { }

                frcStaticFileDeploy(getArtifactTypeClass("FileTreeArtifact")) {
                    files = project.fileTree("src/main/deploy")
                    directory = "/home/lvuser/deploy"
                }
            }
        }
    }
}

def deployArtifact = deploy.targets.roborio.artifacts.frcJava

// Set to true to use debug for JNI
wpi.java.debugJni = false

dependencies {
    //implementation project(":framework")
    // WPILib core dependencies
    api wpi.java.deps.wpilib()
    api wpi.java.vendor.java()

    // RoboRIO dependencies
    roborioDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.roborio)
    roborioDebug wpi.java.vendor.jniDebug(wpi.platforms.roborio)

    roborioRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.roborio)
    roborioRelease wpi.java.vendor.jniRelease(wpi.platforms.roborio)

    // Desktop/Simulation dependencies
    nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
    nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
    simulationDebug wpi.sim.enableDebug()

    nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
    nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
    simulationRelease wpi.sim.enableRelease()

    // Additional libraries
    compileOnly "org.jetbrains:annotations:23.0.0"
    implementation "it.unimi.dsi:fastutil:8.2.1"
    implementation "gov.nist.math:jama:1.0.3"

    // WPILib new commands, unneeded but leave it here incase
    //def apiVersion = wpi.versions.wpilibVersion.get()
    //api "edu.wpi.first.wpilibNewCommands:wpilibNewCommands-java:$apiVersion"
}

// Simulation configuration
wpi.sim.addGui().defaultEnabled = true
wpi.sim.addDriverstation()

// JAR configuration
jar {
    //dependsOn ":framework:build"
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

// Configure jar and deploy tasks
deployArtifact.jarTask = jar
wpi.java.configureExecutableTasks(jar)